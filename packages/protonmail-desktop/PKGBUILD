# Maintainer: 
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
pkgname=protonmail-desktop
pkgver=1.2.1
#pkgver=${_pkgver//-/.}
pkgrel=1.1
pkgdesc="Proton official desktop application for Proton Mail and Proton Calendar"
arch=('x86_64')
url="https://proton.me"
license=('unknown')
depends=(
  'alsa-lib'
  'gtk3'
  'libnotify'
  'nss'
)
optdepends=(
  'kde-cli-tools: file deletion support (kioclient5)'
  'pipewire: WebRTC desktop sharing under Wayland'
  'trash-cli: file deletion support (trash-put)'
  'xdg-utils: open URLs with desktopâ€™s default (xdg-email, xdg-open)'
)
source=("https://proton.me/download/mail/linux/${pkgver}/ProtonMail-desktop-beta.deb")
sha256sums=('13b5860c87d5ada434708e5006145708d1b2d756d317dcb432c19f7cf98f7e9c')

package() {
  bsdtar -xvf data.tar.xz -C "$pkgdir/"

  # Move main files to proper directory
  install -d "$pkgdir/opt/"
  mv "$pkgdir"/usr/lib/* "$pkgdir/opt"

  # Replace binary symlink
  ln -sf /opt/proton-mail/Proton\ Mail\ Beta "$pkgdir/usr/bin/proton-mail"

  # Remove unneeded Electron license & lintian folder
  rm -rf "$pkgdir"/usr/share/{doc,lintian}
}
